using Kinectitude.Render.dll
{
    define RenderService as Kinectitude.Render.RenderService
    define RenderManager as Kinectitude.Render.RenderManager
    define RenderComponent as Kinectitude.Render.RenderComponent
    define ImageRenderComponent as Kinectitude.Render.ImageRenderComponent
    define TextRenderComponent as Kinectitude.Render.TextRenderComponent
}

using Kinectitude.Kinect.dll
{
    define KinectFollowComponent as Kinectitude.Kinect.KinectFollowComponent
}

using Kinectitude.Physics.dll
{
    define PhysicsComponent as Kinectitude.Physics.PhysicsComponent
    define PhysicsManager as Kinectitude.Physics.PhysicsManager
    define CollisionEvent as Kinectitude.Physics.CollisionEvent
    define CrossesLineEvent as Kinectitude.Physics.CrossesLineEvent
}

using Kinectitude.Input.dll
{
    define MouseFollowComponent as Kinectitude.Input.MouseFollowComponent
    define MouseClickEvent as Kinectitude.Input.MouseClickEvent
    define KeyEvent as Kinectitude.Input.KeyEvent
    define MouseManager as Kinectitude.Input.MouseManager
    define KeyboardFollowComponent as Kinectitude.Input.KeyboardFollowComponent
}

Game(Name = "Asteroid Defense", FirstScene = "Gameplay")
{
    Prototype explosion()
    {
        Component Transform(Width = 64, Height = 48)
        Component ImageRenderComponent(Image = "explosion.png", Animated = True, Duration = 3, Stretched = False)
        Component PhysicsComponent(Shape = "Ellipse", BodyType = "Static", IgnoreCollisionsWith = $missile + $missileTarget)
        
        Event OnCreate()
        {
            myId = scene.nextExplosionId
            scene.nextExplosionId += 1
            Transform.X = scene.lastMissileX
            Transform.Y = scene.lastMissileY - 30
            CreateTimer(Name = "explosionTimer" + myId, Duration = 3, Trigger = "destroyExplosion", Recurring = False)
        }

        Event TriggerOccurs(Trigger = "destroyExplosion")
        {
            if ((myId < scene.nextExplosionId))
            {
                Destroy()
            }
        }
    }
    
    Prototype asteroid()
    {
        Component Transform(Width = 64, Height = 64)
        Component ImageRenderComponent(Image = "asteroid.png")
        Component PhysicsComponent(Shape = "Ellipse", Mass = 10, AngularVelocity = 1, IgnoreCollisionsWith = $missile + $missileTarget + $asteroid + $launcher)
        
        Event OnCreate()
        {
            Transform.X = Random(0, 1280)
            Transform.Y = -50
            PointTowards(X = Random(0, 1280), Y = 720)
            speedRand = Random(0, 100)
            if ((speedRand < 70))
            {
                PhysicsComponent.Speed = 7
            }
            else if (speedRand < 90)
            {
                PhysicsComponent.Speed = 14
            }
            else
            {
                PhysicsComponent.Speed = 21
            }
        }

        Event CollisionEvent(CollidesWith = $explosion)
        {
            scene.destroyed += 1
            Destroy()
        }

        Event CollisionEvent(CollidesWith = $earthTarget)
        {
            Destroy()
        }
    }
    
    Prototype earthTarget()
    {
        Component Transform()
        Component PhysicsComponent(BodyType = "Static")
        
        Event CollisionEvent(CollidesWith = $asteroid)
        {
            scene.health -= 0.01
        }
    }
    
    Prototype missileTarget()
    {
        Component Transform(Width = 32, Height = 32)
        Component PhysicsComponent(Shape = "Ellipse", BodyType = "Static", IgnoreCollisionsWith = $asteroid + $explosion)
        
        Event OnCreate()
        {
            Transform.X = scene.currentTargetX
            Transform.Y = scene.currentTargetY
        }

        Event CollisionEvent(CollidesWith = $missile)
        {
            Destroy()
        }
    }
    
    Prototype missile()
    {
        Component Transform(Width = 60, Height = 13)
        Component ImageRenderComponent(Image = "missile.png")
        Component PhysicsComponent(IgnoreCollisionsWith = $earthTarget + $asteroid + $explosion + $launcher)
        
        Event OnCreate()
        {
            Transform.X = scene.launchX
            Transform.Y = 750
            PointTowards(X = scene.currentTargetX, Y = scene.currentTargetY)
            PhysicsComponent.Speed = 80
        }

        Event CollisionEvent(CollidesWith = $missileTarget)
        {
            scene.lastMissileX = Transform.X
            scene.lastMissileY = Transform.Y
            CreateEntity(Prototype = "explosion")
            Destroy()
        }
    }
    
    Prototype generalCursor()
    {
        Component Transform(Width = 48, Height = 48)
        Component ImageRenderComponent(Image = "target.png", Animated = False)
        Component KinectFollowComponent(ScaleWidth = 1280, ScaleHeight = 720, SkeletonMaxX = 0.2, SkeletonMaxY = 0.2, IgnoresPhysics = True)
    }
    
    Prototype paddle()
    {
        Component Transform(Width = 32, Height = 128)
        Component RenderComponent(FillColor = "Blue")
        Component KeyboardFollowComponent(Up = "Up", Down = "Down", Speed = 5)
    }
    
    Service RenderService(Width = 1280, Height = 720)
    
    Scene Gameplay(health = 1)
    {
        Manager RenderManager(Width = 1280, Height = 720)        
        Manager MouseManager()        
        Manager PhysicsManager()
        
        Entity stars()
        {
            Component Transform(X = 640, Y = 360, Width = 1280, Height = 720)
            Component ImageRenderComponent(Image = "starfield.png")
        }
        
        Entity cursor : generalCursor()
        {
            Component Transform(X = 308, Y = 192)
            
            Event TriggerOccurs(Trigger = "launch")
            {
                scene.currentTargetX = Transform.X
                scene.currentTargetY = Transform.Y
                if ((scene.currentTargetX < 426))
                {
                    scene.launchX = 213
                }
                else if (scene.currentTargetX < 852)
                {
                    scene.launchX = 639
                }
                else
                {
                    scene.launchX = 1065
                }
                CreateEntity(Prototype = "missileTarget")
                CreateEntity(Prototype = "missile")
            }
        }
        
        Entity events()
        {
            Component Transform(X = 595, Y = 617, Width = 48, Height = 48)
            
            Event SceneStarts()
            {
                CreateTimer(Name = "asteroidTimer", Duration = 2.2, Trigger = "makeAsteroid")
            }

            Event TriggerOccurs(Trigger = "makeAsteroid")
            {
                asteroids += 1
                CreateEntity(Prototype = "asteroid")
            }

            Event KeyEvent(Key = "Escape")
            {
                Quit()
            }
        }
        
        Entity healthBackground()
        {
            Component Transform(X = 640, Y = 32, Width = 600, Height = 32)
            Component RenderComponent(Shape = "Rectangle", FillColor = "DarkGray")
        }
        
        Entity healthCounter()
        {
            Component Transform(X = 640, Y = 32, Width = 600, Height = 32)
            Component RenderComponent(Shape = "Rectangle", FillColor = "Blue")
            
            Event AttributeChanges(Target = scene.health)
            {
                Transform.Width = scene.health * 600
                Transform.X -= 3
            }
        }
        
        Entity : earthTarget()
        {
            Component Transform(X = 640, Y = 562, Width = 1280, Height = 16)
            
            Event CollisionEvent(CollidesWith = $asteroid)
            {
                scene.health -= 0.01
            }
        }
        
        Entity destroyed()
        {
            Component Transform(X = 18, Y = 18)
            Component TextRenderComponent(Value = "DESTROYED: 0", FontWeight = "Bold", FontSize = 24, FontColor = "#ffffff")
            
            Event AttributeChanges(Target = scene.destroyed)
            {
                TextRenderComponent.Value = "DESTROYED: " + scene.destroyed
            }
        }
        
        Entity launcher()
        {
            Component Transform(X = 138, Y = 326, Width = 48, Height = 48)
            Component KinectFollowComponent(Joint = "HandLeft", ScaleWidth = 1280, ScaleHeight = 720, SkeletonMaxX = 0.2, SkeletonMaxY = 0.2, IgnoresPhysics = True)
            Component PhysicsComponent(BodyType = "Kinematic", IgnoreCollisionsWith = $asteroid + $missile)
            
            Event AttributeChanges(Target = Transform.Y)
            {
                if (Transform.Y < 360)
                {
					if (prevY >= 360)
					{
						FireTrigger(Name = "launch")
					}
                }
                prevY = Transform.Y
            }
        }
    }
    
    Scene Menu()
    {
        Entity ()
        {
            Component Transform(X = 640, Y = 360, Width = 1280, Height = 720)
            Component ImageRenderComponent(Image = "starfield.png")
        }
        
        Entity title()
        {
            Component Transform(X = 640, Y = 120, Width = 278, Height = 150)
            Component ImageRenderComponent(Image = "title.png", Opacity = 1)
        }
        
        Entity cursor : generalCursor()
        {
            Component Transform(X = 560, Y = 295)
        }
    }
    
    Scene Test()
    {
        Entity : paddle()
        {
            Component Transform(X = 239, Y = 306)
        }
    }
}